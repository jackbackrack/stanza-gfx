;; See license.txt for details about licensing.

defpackage gfx/drawing :
  import core
  import math
  import collections
  ; import utils
  import geom/vec
  import geom/box
  import geom/polyline
  import gfx/gl
  import gfx/font

public defn gl-color (v:V3f) :
  gl-color3f(x(v), y(v), z(v))

public defn gl-color (v:V4f) :
  gl-color4f(x(v), y(v), z(v), a(v))

public defn gl-vertex (v:V3f) :
  gl-vertex3f(x(v), y(v), z(v))

public defn gl-translate (v:V3f) :
  gl-translatef(x(v), y(v), z(v))

public defn gl-scale (v:V3f) :
  gl-scalef(x(v), y(v), z(v))

public defn gl-rotate (a:Float, v:V3f) :
  gl-rotatef(a, x(v), y(v), z(v))

public defn gl-vertex (v:V2f) :
  gl-vertex2f(x(v), y(v))

public defn gl-translate (v:V2f) :
  gl-translatef(x(v), y(v), 0.0f)

public defn gl-scale (v:V2f) :
  gl-scalef(x(v), y(v), 1.0f)

defn do-draw-rect3 (w:Float, h:Float, d:Float, k:Int) :
  gl-push-matrix()
  gl-scalef(w * 0.5f, h * 0.5f, d * 0.5f)
  gl-begin(k)
  gl-vertex3f(-1.0f, -1.0f, -1.0f)
  gl-vertex3f(-1.0f,  1.0f, -1.0f)
  gl-vertex3f( 1.0f,  1.0f, -1.0f)
  gl-vertex3f( 1.0f, -1.0f, -1.0f)
  gl-vertex3f(-1.0f, -1.0f,  1.0f)
  gl-vertex3f(-1.0f,  1.0f,  1.0f)
  gl-vertex3f( 1.0f,  1.0f,  1.0f)
  gl-vertex3f( 1.0f, -1.0f,  1.0f)

  gl-vertex3f(-1.0f, -1.0f, -1.0f)
  gl-vertex3f(-1.0f, -1.0f,  1.0f)
  gl-vertex3f(-1.0f,  1.0f,  1.0f)
  gl-vertex3f(-1.0f,  1.0f, -1.0f)
  gl-vertex3f( 1.0f, -1.0f, -1.0f)
  gl-vertex3f( 1.0f, -1.0f,  1.0f)
  gl-vertex3f( 1.0f,  1.0f,  1.0f)
  gl-vertex3f( 1.0f,  1.0f, -1.0f)

  gl-vertex3f(-1.0f, -1.0f, -1.0f)
  gl-vertex3f(-1.0f, -1.0f,  1.0f)
  gl-vertex3f( 1.0f, -1.0f,  1.0f)
  gl-vertex3f( 1.0f, -1.0f, -1.0f)
  gl-vertex3f(-1.0f   1.0f, -1.0f)
  gl-vertex3f(-1.0f   1.0f,  1.0f)
  gl-vertex3f( 1.0f   1.0f,  1.0f)
  gl-vertex3f( 1.0f   1.0f, -1.0f)

  gl-end()
  gl-pop-matrix()

public defn draw-rect3 (w:Float, h:Float, d:Float) : do-draw-rect3(w, h, d, GL-LINE-LOOP)
public defn draw-quad3 (w:Float, h:Float, d:Float) : do-draw-rect3(w, h, d, GL-QUADS)

defn do-draw-rect (w:Float, h:Float, k:Int) :
  gl-push-matrix()
  gl-scalef(w * 0.5f, h * 0.5f, 1.0f)
  gl-begin(k)
  gl-vertex2f(-1.0f, -1.0f)
  gl-vertex2f(-1.0f,  1.0f)
  gl-vertex2f( 1.0f,  1.0f)
  gl-vertex2f( 1.0f, -1.0f)
  gl-end()
  gl-pop-matrix()

public defn draw-rect (w:Float, h:Float) : do-draw-rect(w, h, GL-LINE-LOOP)
public defn draw-quad (w:Float, h:Float) : do-draw-rect(w, h, GL-QUADS)

val N-CIRCLE-VERTICES = 16
val circle-vertices = Vector<V3f>()
for i in 0 to N-CIRCLE-VERTICES do :
  val f = to-float(i) / to-float(N-CIRCLE-VERTICES)
  val t = 2.0f * PI-F * (1.0f - f)
  add(circle-vertices, V3f(sin(t), cos(t), 0.0f))

public defn draw-circle (r:Float) :
  gl-push-matrix()
  gl-scalef(r, r, r)
  gl-begin(GL-LINE-LOOP)
  for v in circle-vertices do :
    gl-vertex(v)
  gl-end()
  gl-pop-matrix()

public defn draw-circle (r:Float, n:Int) :
  gl-push-matrix()
  gl-scalef(r, r, r)
  gl-begin(GL-LINE-LOOP)
  for i in 0 to n do :
    val a = to-float(i) * 2.0f * PI-F / to-float(n)
    gl-vertex2f(cos(a), sin(a))
  gl-end()
  gl-pop-matrix()

public defn draw-segment (src:V2f, dst:V2f, rad:Float) :
  val v = normalize(dst - src)
  val u = rad * V2f(   y(v), (- x(v)))
  val d = rad * V2f((- y(v)),   x(v))
  gl-begin(GL-QUADS)
  gl-vertex(src + u)
  gl-vertex(src + d)
  gl-vertex(dst + d)
  gl-vertex(dst + u)
  gl-end()

public defn draw-disk (r:Float) :
  gl-push-matrix()
  gl-scalef(r, r, 1.0f)
  gl-begin(GL-TRIANGLES)
  for i in 0 to N-CIRCLE-VERTICES do :
    val j = (i + 1) % N-CIRCLE-VERTICES
    gl-vertex(circle-vertices[i])
    gl-vertex(circle-vertices[j])
    gl-vertex2f(0.0f, 0.0f)
  gl-end()
  gl-pop-matrix()

public defn draw-fat-line (a:V2f, b:V2f, t:Float) :
  gl-push-matrix()
  gl-translate(a)
  draw-disk(t)
  gl-pop-matrix()
  draw-segment(a, b, t)
  gl-push-matrix()
  gl-translate(b)
  draw-disk(t)
  gl-pop-matrix()

public defn draw-cylinder (h:Float, r:Float) :
  val ph = 0.5f * h
  val nh = (- ph)
  gl-push-matrix()
  gl-translatef(0.0f, 0.0f, nh)
  draw-disk(r)
  gl-pop-matrix()
  gl-push-matrix()
  gl-translatef(0.0f, 0.0f, ph)
  draw-disk(r)
  gl-pop-matrix()
  gl-push-matrix()
  gl-scalef(r, r, 1.0f)
  gl-begin(GL-TRIANGLES)
  for i in 0 to N-CIRCLE-VERTICES do :
    val j = (i + 1) % N-CIRCLE-VERTICES
    val vi = circle-vertices[i]
    val vj = circle-vertices[j]
    gl-vertex3f(x(vi), y(vi), nh)
    gl-vertex3f(x(vj), y(vj), nh)
    gl-vertex3f(x(vj), y(vj), ph)
    gl-vertex3f(x(vj), y(vj), ph)
    gl-vertex3f(x(vi), y(vi), ph)
    gl-vertex3f(x(vi), y(vi), nh)
  gl-end()
  gl-pop-matrix()

public defn stroke (pl:PolyLine2f) :
  for line in strokes(pl) do :
    gl-begin(GL-LINE-STRIP)
    do(gl-vertex, line)
    gl-end()

;; TODO: NEED TO WAIT TO USE DISPLAY LISTS UNTIL AFTER INIT OR SOMETHING
public var glyph-stroking?:True|False = false

public defn stroke-char (glyph:Glyph) :
  if glyph-stroking? :
     stroke(polyline(glyph))
  else :
    if null?(display-list(glyph)) :
      val dl = gl-gen-list()
      gl-new-list(dl, GL-COMPILE)
      stroke(polyline(glyph))
      gl-end-list(dl)
      set-display-list(glyph, dl)
    gl-call-list(display-list(glyph))
  
defn stroke-text (s:Seqable<Glyph>) :
  for g in s do :
    val w = width(g)
    gl-translatef(0.5f * w, 0.0f, 0.0f)
    stroke-char(g)
    gl-translatef(0.5f * w, 0.0f, 0.0f)

defn draw-text-at (tx:Float, ty:Float, sx:Float, sy:Float, s:Seqable<Glyph>) :
  gl-push-matrix()
  gl-translatef(tx, ty, 0.0f)
  gl-scalef(sx, sy, 1.0f)
  stroke-text(s)
  gl-pop-matrix()

public defn draw-text-mag (w:Float, ts:Float, font:Font, s:Collection<Char>&Lengthable) :
  val aw = glyphs-width(ts, font, s)
  draw-text-at(-0.5f * aw, 0.0f, ts, ts, seq({ get-glyph(font, _) }, s))

public defn draw-text-left-mag (w:Float, ts:Float, font:Font, s:Collection<Char>&Lengthable) :
  val aw = glyphs-width(ts, font, s)
  draw-text-at(-0.5f * w, 0.0f, ts, ts, seq({ get-glyph(font, _) }, s))

public defn draw-text-right-mag (w:Float, ts:Float, font:Font, s:Collection<Char>&Lengthable) :
  val aw = glyphs-width(ts, font, s)
  draw-text-at(0.5f * w - aw, 0.0f, ts, ts, seq({ get-glyph(font, _) }, s))

public defn draw-text (w:Float, h:Float, font:Font, dir:Symbol, s:Collection<Char>&Lengthable) :
  val ts = glyphs-mag(w, h, font, s)
  val aw = glyphs-width(ts, font, s)
  val ah = height(font) * ts
  val p  = switch(dir) :
    `C  : V2f(-0.5f * aw,      0.0f)
    `W  : V2f(-0.5f * w,       0.0f)
    `E  : V2f( 0.5f * w - aw,  0.0f)
    `N  : V2f(-0.5f * aw,     -0.5f * (h - ah))
    `S  : V2f(-0.5f * aw,      0.5f * (h - ah))
    `NW : V2f(-0.5f * w,      -0.5f * (h - ah))
    `NE : V2f( 0.5f * w - aw, -0.5f * (h - ah))
    `SW : V2f(-0.5f * w,       0.5f * (h - ah))
    `SE : V2f( 0.5f * w - aw,  0.5f * (h - ah))
  draw-text-at(x(p), y(p), ts, ts, seq({ get-glyph(font, _) }, s))

public defn draw-text (w:Float, h:Float, font:Font, s:Collection<Char>&Lengthable) :
  draw-text-mag(w, glyphs-mag(w, h, font, s), font, s)

public defn draw-text-left (w:Float, h:Float, font:Font, s:Collection<Char>&Lengthable) :
  draw-text-left-mag(w, glyphs-mag(w, h, font, s), font, s)

public defn draw-text-right (w:Float, h:Float, font:Font, s:Collection<Char>&Lengthable) :
  draw-text-right-mag(w, glyphs-mag(w, h, font, s), font, s)

defn clip (x:Float, mn:Float, mx:Float) : max(mn, min(mx, x))

;; Convert hue/saturation/value to red/green/blue.  Output returned in args.
public defn hsv-to-rgb (h:Float, sa:Float, v:Float) -> [Float, Float, Float] :
  val s = clip(sa, 0.0f, 1.0f);
  val [rt, gt, bt] = 
    if s == 0.0f :
      [v, v, v]
    else :
      val h_temp = (if h == 360.0f : 0.0f else: h) / 60.0f
      val i = to-int(h_temp)
      val f = h_temp - to-float(i)
      val p = v * (1.0f - s)
      val q = v * (1.0f - (s * f))
      val t = v * (1.0f - (s * (1.0f - f)));
      if i == 0 : [v, t, p]
      else if i == 1 : [q, v, p]
      else if i == 2 : [p, v, t]
      else if i == 3 : [p, q, v]
      else if i == 4 : [v, p, q]
      else  : [v, p, q] ;; if i == 5
  [clip(rt, 0.0f, 255.0f), clip(gt, 0.0f, 255.0f), clip(bt, 0.0f, 255.0f)]

public defn hsv-to-rgb (hsv:V3f) -> V3f :
  val [r, g, b] = hsv-to-rgb(x(hsv), y(hsv), z(hsv))
  V3f(r, g, b)

;; Convert hue/saturation/value to red/green/blue.  Output returned in args.
public defn rgb-to-hsv (r:Float, g:Float, b:Float) -> [Float, Float, Float] :
  val mn    = min(r, min(g, b));
  val mx    = max(r, max(g, b));
  val delta = mx - mn
  val v     = mx / 255.0f
  if mx == 0.0f :
    [0.0f, 0.0f, v]
  else :
    val s = delta / mx
    val h0 = 
      if r == mx :
        (g - b) / delta
      else if g == mx :
        2.0f + (b - r) / delta
      else :
        4.0f + (r - g) / delta
    val h1 = h0 * 60.0f
    val h = if h1 < 0.0f : h1 + 360.f else : h1
    [h, s, v]

public defn rgb-to-hsv (rgb:V3f) -> V3f :
  val [h, s, v] = rgb-to-hsv(x(rgb), y(rgb), z(rgb))
  V3f(h, s, v)

