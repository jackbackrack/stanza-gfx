;; See license.txt for details about licensing.

defpackage gfx/drawing :
  import core
  import math
  import collections
  ; import utils
  import geom/vec
  import geom/mat
  import geom/box
  import geom/polyline
  import gfx/gl
  import gfx/font

public defn gl-color (v:Vec3) :
  gl-color(x(v), y(v), z(v))

public defn gl-color (v:Vec4) :
  gl-color(x(v), y(v), z(v), a(v))

public defn gl-vertex (v:Vec3) :
  gl-vertex(x(v), y(v), z(v))

public defn gl-translate (v:Vec3) :
  gl-translate(x(v), y(v), z(v))

public defn gl-scale (v:Vec3) :
  gl-scale(x(v), y(v), z(v))

public defn gl-rotate (a:Double, v:Vec3) :
  gl-rotate(a, x(v), y(v), z(v))

public defn gl-vertex (v:Vec2) :
  gl-vertex(x(v), y(v))

public defn gl-translate (v:Vec2) :
  gl-translate(x(v), y(v), 0.0)

public defn gl-scale (v:Vec2) :
  gl-scale(x(v), y(v), 1.0)

defn do-draw-rect3 (w:Double, h:Double, d:Double, k:Int) :
  gl-push-matrix()
  gl-scale(w * 0.5, h * 0.5, d * 0.5)
  gl-begin(k)
  gl-vertex(-1.0, -1.0, -1.0)
  gl-vertex(-1.0,  1.0, -1.0)
  gl-vertex( 1.0,  1.0, -1.0)
  gl-vertex( 1.0, -1.0, -1.0)
  gl-end()
  gl-begin(k)
  gl-vertex(-1.0, -1.0,  1.0)
  gl-vertex(-1.0,  1.0,  1.0)
  gl-vertex( 1.0,  1.0,  1.0)
  gl-vertex( 1.0, -1.0,  1.0)
  gl-end()
  gl-begin(k)
  gl-vertex(-1.0, -1.0, -1.0)
  gl-vertex(-1.0, -1.0,  1.0)
  gl-vertex(-1.0,  1.0,  1.0)
  gl-vertex(-1.0,  1.0, -1.0)
  gl-end()
  gl-begin(k)
  gl-vertex( 1.0, -1.0, -1.0)
  gl-vertex( 1.0, -1.0,  1.0)
  gl-vertex( 1.0,  1.0,  1.0)
  gl-vertex( 1.0,  1.0, -1.0)
  gl-end()
  gl-begin(k)
  gl-vertex(-1.0, -1.0, -1.0)
  gl-vertex(-1.0, -1.0,  1.0)
  gl-vertex( 1.0, -1.0,  1.0)
  gl-vertex( 1.0, -1.0, -1.0)
  gl-end()
  gl-begin(k)
  gl-vertex(-1.0   1.0, -1.0)
  gl-vertex(-1.0   1.0,  1.0)
  gl-vertex( 1.0   1.0,  1.0)
  gl-vertex( 1.0   1.0, -1.0)
  gl-end()
  gl-pop-matrix()

public defn draw-rect3 (w:Double, h:Double, d:Double) : do-draw-rect3(w, h, d, GL-LINE-LOOP)
public defn draw-quad3 (w:Double, h:Double, d:Double) : do-draw-rect3(w, h, d, GL-QUADS)

defn do-draw-rect (w:Double, h:Double, k:Int) :
  gl-push-matrix()
  gl-scale(w * 0.5, h * 0.5, 1.0)
  gl-begin(k)
  gl-vertex(-1.0, -1.0)
  gl-vertex(-1.0,  1.0)
  gl-vertex( 1.0,  1.0)
  gl-vertex( 1.0, -1.0)
  gl-end()
  gl-pop-matrix()

public defn draw-rect (w:Double, h:Double) : do-draw-rect(w, h, GL-LINE-LOOP)
public defn draw-quad (w:Double, h:Double) : do-draw-rect(w, h, GL-QUADS)

val N-CIRCLE-VERTICES = 16
val circle-vertices = Vector<Vec3>()
for i in 0 to N-CIRCLE-VERTICES do :
  val f = to-double(i) / to-double(N-CIRCLE-VERTICES)
  val t = 2.0 * PI * (1.0 - f)
  add(circle-vertices, Vec3(sin(t), cos(t), 0.0))

public defn draw-circle (r:Double) :
  gl-push-matrix()
  gl-scale(r, r, r)
  gl-begin(GL-LINE-LOOP)
  for v in circle-vertices do :
    gl-vertex(v)
  gl-end()
  gl-pop-matrix()

public defn draw-circle (r:Double, n:Int) :
  gl-push-matrix()
  gl-scale(r, r, r)
  gl-begin(GL-LINE-LOOP)
  for i in 0 to n do :
    val a = to-double(i) * 2.0 * PI / to-double(n)
    gl-vertex(cos(a), sin(a))
  gl-end()
  gl-pop-matrix()

public defn draw-segment (src:Vec2, dst:Vec2, rad:Double) :
  val v = normalize(dst - src)
  val u = rad * Vec2(   y(v), (- x(v)))
  val d = rad * Vec2((- y(v)),   x(v))
  gl-begin(GL-QUADS)
  gl-vertex(src + u)
  gl-vertex(src + d)
  gl-vertex(dst + d)
  gl-vertex(dst + u)
  gl-end()

public defn draw-disk (r:Double) :
  gl-push-matrix()
  gl-scale(r, r, 1.0)
  gl-begin(GL-TRIANGLES)
  for i in 0 to N-CIRCLE-VERTICES do :
    val j = (i + 1) % N-CIRCLE-VERTICES
    gl-vertex(circle-vertices[i])
    gl-vertex(circle-vertices[j])
    gl-vertex(0.0, 0.0)
  gl-end()
  gl-pop-matrix()

public defn draw-fat-line (a:Vec2, b:Vec2, t:Double) :
  gl-push-matrix()
  gl-translate(a)
  draw-disk(t)
  gl-pop-matrix()
  draw-segment(a, b, t)
  gl-push-matrix()
  gl-translate(b)
  draw-disk(t)
  gl-pop-matrix()

public defn draw-cylinder (h:Double, r:Double) :
  val ph = 0.5 * h
  val nh = (- ph)
  gl-push-matrix()
  gl-translate(0.0, 0.0, nh)
  draw-disk(r)
  gl-pop-matrix()
  gl-push-matrix()
  gl-translate(0.0, 0.0, ph)
  draw-disk(r)
  gl-pop-matrix()
  gl-push-matrix()
  gl-scale(r, r, 1.0)
  gl-begin(GL-TRIANGLES)
  for i in 0 to N-CIRCLE-VERTICES do :
    val j = (i + 1) % N-CIRCLE-VERTICES
    val vi = circle-vertices[i]
    val vj = circle-vertices[j]
    gl-vertex(x(vi), y(vi), nh)
    gl-vertex(x(vj), y(vj), nh)
    gl-vertex(x(vj), y(vj), ph)
    gl-vertex(x(vj), y(vj), ph)
    gl-vertex(x(vi), y(vi), ph)
    gl-vertex(x(vi), y(vi), nh)
  gl-end()
  gl-pop-matrix()

public defn stroke (pl:PolyLine2) :
  for line in strokes(pl) do :
    gl-begin(GL-LINE-STRIP)
    do(gl-vertex, line)
    gl-end()

;; TODO: NEED TO WAIT TO USE DISPLAY LISTS UNTIL AFTER INIT OR SOMETHING
public var glyph-stroking?:True|False = false

public defn stroke-char (glyph:Glyph) :
  if glyph-stroking? :
     stroke(polyline(glyph))
  else :
    if null?(display-list(glyph)) :
      val dl = gl-gen-list()
      gl-new-list(dl, GL-COMPILE)
      stroke(polyline(glyph))
      gl-end-list(dl)
      set-display-list(glyph, dl)
    gl-call-list(display-list(glyph))
  
defn stroke-text (s:Seqable<Glyph>) :
  for g in s do :
    val w = width(g)
    gl-translate(0.5 * w, 0.0, 0.0)
    stroke-char(g)
    gl-translate(0.5 * w, 0.0, 0.0)

defn draw-text-at (tx:Double, ty:Double, sx:Double, sy:Double, s:Seqable<Glyph>) :
  gl-push-matrix()
  gl-translate(tx, ty, 0.0)
  gl-scale(sx, sy, 1.0)
  stroke-text(s)
  gl-pop-matrix()

public defn draw-text-mag (w:Double, ts:Double, font:Font, s:Collection<Char>&Lengthable) :
  val aw = glyphs-width(ts, font, s)
  draw-text-at(-0.5 * aw, 0.0, ts, ts, seq({ get-glyph(font, _) }, s))

public defn draw-text-left-mag (w:Double, ts:Double, font:Font, s:Collection<Char>&Lengthable) :
  val aw = glyphs-width(ts, font, s)
  draw-text-at(-0.5 * w, 0.0, ts, ts, seq({ get-glyph(font, _) }, s))

public defn draw-text-right-mag (w:Double, ts:Double, font:Font, s:Collection<Char>&Lengthable) :
  val aw = glyphs-width(ts, font, s)
  draw-text-at(0.5 * w - aw, 0.0, ts, ts, seq({ get-glyph(font, _) }, s))

public defn draw-text (w:Double, h:Double, font:Font, dir:Symbol, s:Collection<Char>&Lengthable) :
  val ts = glyphs-mag(w, h, font, s)
  val aw = glyphs-width(ts, font, s)
  val ah = height(font) * ts
  val p  = switch(dir) :
    `C  : Vec2(-0.5 * aw,      0.0)
    `W  : Vec2(-0.5 * w,       0.0)
    `E  : Vec2( 0.5 * w - aw,  0.0)
    `N  : Vec2(-0.5 * aw,     -0.5 * (h - ah))
    `S  : Vec2(-0.5 * aw,      0.5 * (h - ah))
    `NW : Vec2(-0.5 * w,      -0.5 * (h - ah))
    `NE : Vec2( 0.5 * w - aw, -0.5 * (h - ah))
    `SW : Vec2(-0.5 * w,       0.5 * (h - ah))
    `SE : Vec2( 0.5 * w - aw,  0.5 * (h - ah))
  draw-text-at(x(p), y(p), ts, ts, seq({ get-glyph(font, _) }, s))

public defn draw-text (w:Double, h:Double, font:Font, s:Collection<Char>&Lengthable) :
  draw-text-mag(w, glyphs-mag(w, h, font, s), font, s)

public defn draw-text-left (w:Double, h:Double, font:Font, s:Collection<Char>&Lengthable) :
  draw-text-left-mag(w, glyphs-mag(w, h, font, s), font, s)

public defn draw-text-right (w:Double, h:Double, font:Font, s:Collection<Char>&Lengthable) :
  draw-text-right-mag(w, glyphs-mag(w, h, font, s), font, s)

defn clip (x:Double, mn:Double, mx:Double) : max(mn, min(mx, x))

;; Convert hue/saturation/value to red/green/blue.  Output returned in args.
public defn hsv-to-rgb (h:Double, sa:Double, v:Double) -> [Double, Double, Double] :
  val s = clip(sa, 0.0, 1.0);
  val [rt, gt, bt] = 
    if s == 0.0 :
      [v, v, v]
    else :
      val h_temp = (if h == 360.0 : 0.0 else: h) / 60.0
      val i = to-int(h_temp)
      val f = h_temp - to-double(i)
      val p = v * (1.0 - s)
      val q = v * (1.0 - (s * f))
      val t = v * (1.0 - (s * (1.0 - f)));
      if i == 0 : [v, t, p]
      else if i == 1 : [q, v, p]
      else if i == 2 : [p, v, t]
      else if i == 3 : [p, q, v]
      else if i == 4 : [v, p, q]
      else  : [v, p, q] ;; if i == 5
  [clip(rt, 0.0, 255.0), clip(gt, 0.0, 255.0), clip(bt, 0.0, 255.0)]

public defn hsv-to-rgb (hsv:Vec3) -> Vec3 :
  val [r, g, b] = hsv-to-rgb(x(hsv), y(hsv), z(hsv))
  Vec3(r, g, b)

;; Convert hue/saturation/value to red/green/blue.  Output returned in args.
public defn rgb-to-hsv (r:Double, g:Double, b:Double) -> [Double, Double, Double] :
  val mn    = min(r, min(g, b));
  val mx    = max(r, max(g, b));
  val delta = mx - mn
  val v     = mx / 255.0
  if mx == 0.0 :
    [0.0, 0.0, v]
  else :
    val s = delta / mx
    val h0 = 
      if r == mx :
        (g - b) / delta
      else if g == mx :
        2.0 + (b - r) / delta
      else :
        4.0 + (r - g) / delta
    val h1 = h0 * 60.0
    val h = if h1 < 0.0 : h1 + 360.0 else : h1
    [h, s, v]

public defn rgb-to-hsv (rgb:Vec3) -> Vec3 :
  val [h, s, v] = rgb-to-hsv(x(rgb), y(rgb), z(rgb))
  Vec3(h, s, v)

